{
    var mssgStack=[];
    
    function addWarning( text, alocation ){
        mssgStack.push(
        {
            message: text,
            location: alocation,
            type: "warning"
        });
    };
    
    
    
    function addError( text, alocation ){
        mssgStack.push(
        {
            message: text,
            location: alocation,
            type: "error"
        });
    };
    
    function clearMssgs(){
        mssgStack=[];
    };
    
    
    Array.prototype.hasValue = function(value) {
      var i;
      for (i=0; i<this.length; i++) { if (this[i] === value) return true; }
      return false;
    };
    
var allElements = [
"feComponentTransfer", "feSpecularLighting", "feDiffuseLighting", "feDisplacementMap", "animateTransform", "feConvolveMatrix", "font.face.format", "feDistantLight", "feGaussianBlur", "font.face.name", "linearGradient", "radialGradient", "animateMotion", "color.profile", "feColorMatrix", "font.face.src", "font.face.uri", "foreignObject", "missing.glyph", "altGlyphItem", "animateColor", "feMorphology", "fePointLight", "feTurbulence", "altGlyphDef", "feComposite", "feMergeNode", "feSpotLight", "font.face", "altGlyph", "clipPath", "feOffset", "glyphRef", "metadata", "polyline", "textPath", "animate", "ellipse", "feBlend", "feFlood", "feFuncA", "feFuncB", "feFuncG", "feFuncR", "feImage", "feMerge", "pattern", "polygon", "circle", "cursor", "feTile", "filter", "marker", "script", "switch", "symbol", "glyph", "hkern", "image", "mpath", "style", "title", "tspan", "vkern", "defs", "desc", "font", "line", "mask", "path", "rect", "stop", "text", "tref", "view", "set", "svg", "use", "a", "g"
];

 var acceptContentEle = { 
'a' : [ "animate", "set", "animateMotion", "animateColor", "animateTransform", "desc", "title", "metadata", "rect", "circle", "ellipse", "line", "polyline", "polygon", "path", "svg", "g", "defs", "symbol", "use", "linearGradient", "radialGradient", "a", "altGlyphDef", "clipPath", "color.profile", "cursor", "filter", "font", "font.face", "foreignObject", "image", "marker", "mask", "pattern", "script", "style", "switch", "text", "view" ],
'view' : [ "desc", "title", "metadata" ],
'text' : [ "animate", "set", "animateMotion", "animateColor", "animateTransform", "desc", "title", "metadata", "tspan", "tref", "textPath", "altGlyph", "a" ],
'tspan' : [ "desc", "title", "metadata", "a", "altGlyph", "animate", "animateColor", "set", "tref", "tspan" ],
'tref' : [ "desc", "title", "metadata", "animate", "animateColor", "set" ],
'textPath' : [ "desc", "title", "metadata", "a", "altGlyph", "animate", "animateColor", "set", "tref", "tspan" ],
'altGlyphDef' : [ "glyphRef", "altGlyphItem" ],
'altGlyphItem' : [ "glyphRef" ],
'glyphRef' : [ "Empty." ],
'animate' : [ "desc", "title", "metadata" ],
'set' : [ "desc", "title", "metadata" ],
'animateMotion' : [ "desc", "title", "metadata", "mpath" ],
'mpath' : [ "desc", "title", "metadata" ],
'animateColor' : [ "desc", "title", "metadata" ],
'animateTransform' : [ "desc", "title", "metadata" ],
'rect' : [ "animate", "set", "animateMotion", "animateColor", "animateTransform", "desc", "title", "metadata" ],
'circle' : [ "animate", "set", "animateMotion", "animateColor", "animateTransform", "desc", "title", "metadata" ],
'ellipse' : [ "animate", "set", "animateMotion", "animateColor", "animateTransform", "desc", "title", "metadata" ],
'line' : [ "animate", "set", "animateMotion", "animateColor", "animateTransform", "desc", "title", "metadata" ],
'polyline' : [ "animate", "set", "animateMotion", "animateColor", "animateTransform", "desc", "title", "metadata" ],
'polygon' : [ "animate", "set", "animateMotion", "animateColor", "animateTransform", "desc", "title", "metadata" ],
'clipPath' : [ "desc", "title", "metadata", "animate", "set", "animateMotion", "animateColor", "animateTransform", "rect", "circle", "ellipse", "line", "polyline", "polygon", "path", "text", "use" ],
'mask' : [ "animate", "set", "animateMotion", "animateColor", "animateTransform", "desc", "title", "metadata", "rect", "circle", "ellipse", "line", "polyline", "polygon", "path", "svg", "g", "defs", "symbol", "use", "linearGradient", "radialGradient", "a", "altGlyphDef", "clipPath", "color.profile", "cursor", "filter", "font", "font.face", "foreignObject", "image", "marker", "mask", "pattern", "script", "style", "switch", "text", "view" ],
'color.profile' : [ "desc", "title", "metadata" ],
'cursor' : [ "desc", "title", "metadata" ],
'svg' : [ "animate", "set", "animateMotion", "animateColor", "animateTransform", "desc", "title", "metadata", "rect", "circle", "ellipse", "line", "polyline", "polygon", "path", "svg", "g", "defs", "symbol", "use", "linearGradient", "radialGradient", "a", "altGlyphDef", "clipPath", "color.profile", "cursor", "filter", "font", "font.face", "foreignObject", "image", "marker", "mask", "pattern", "script", "style", "switch", "text", "view" ],
'g' : [ "animate", "set", "animateMotion", "animateColor", "animateTransform", "desc", "title", "metadata", "rect", "circle", "ellipse", "line", "polyline", "polygon", "path", "svg", "g", "defs", "symbol", "use", "linearGradient", "radialGradient", "a", "altGlyphDef", "clipPath", "color.profile", "cursor", "filter", "font", "font.face", "foreignObject", "image", "marker", "mask", "pattern", "script", "style", "switch", "text", "view" ],
'defs' : [ "animate", "set", "animateMotion", "animateColor", "animateTransform", "desc", "title", "metadata", "rect", "circle", "ellipse", "line", "polyline", "polygon", "path", "svg", "g", "defs", "symbol", "use", "linearGradient", "radialGradient", "a", "altGlyphDef", "clipPath", "color.profile", "cursor", "filter", "font", "font.face", "foreignObject", "image", "marker", "mask", "pattern", "script", "style", "switch", "text", "view" ],
'symbol' : [ "animate", "set", "animateMotion", "animateColor", "animateTransform", "desc", "title", "metadata", "rect", "circle", "ellipse", "line", "polyline", "polygon", "path", "svg", "g", "defs", "symbol", "use", "linearGradient", "radialGradient", "a", "altGlyphDef", "clipPath", "color.profile", "cursor", "filter", "font", "font.face", "foreignObject", "image", "marker", "mask", "pattern", "script", "style", "switch", "text", "view" ],
'use' : [ "animate", "set", "animateMotion", "animateColor", "animateTransform", "desc", "title", "metadata" ],
'image' : [ "animate", "set", "animateMotion", "animateColor", "animateTransform", "desc", "title", "metadata" ],
'switch' : [ "animate", "set", "animateMotion", "animateColor", "animateTransform", "desc", "title", "metadata", "rect", "circle", "ellipse", "line", "polyline", "polygon", "path", "a", "foreignObject", "g", "image", "svg", "switch", "text", "use" ],
'filter' : [ "desc", "title", "metadata", "feBlend", "feColorMatrix", "feComponentTransfer", "feComposite", "feConvolveMatrix", "feDiffuseLighting", "feDisplacementMap", "feFlood", "feGaussianBlur", "feImage", "feMerge", "feMorphology", "feOffset", "feSpecularLighting", "feTile", "feTurbulence", "animate", "set" ],
'feDistantLight' : [ "animate", "set" ],
'fePointLight' : [ "animate", "set" ],
'feSpotLight' : [ "animate", "set" ],
'feBlend' : [ "animate", "set" ],
'feColorMatrix' : [ "animate", "set" ],
'feComponentTransfer' : [ "feFuncA", "feFuncB", "feFuncG", "feFuncR" ],
'feFuncR' : [ "animate", "set" ],
'feFuncG' : [ "animate", "set" ],
'feFuncB' : [ "animate", "set" ],
'feFuncA' : [ "animate", "set" ],
'feComposite' : [ "animate", "set" ],
'feConvolveMatrix' : [ "animate", "set" ],
'feDiffuseLighting' : [ "desc", "title", "metadata", "feDistantLight", "fePointLight", "feSpotLight" ],
'feDisplacementMap' : [ "animate", "set" ],
'feFlood' : [ "animate", "animateColor", "set" ],
'feGaussianBlur' : [ "animate", "set" ],
'feImage' : [ "animate", "animateTransform", "set" ],
'feMerge' : [ "feMergeNode" ],
'feMergeNode' : [ "animate", "set" ],
'feMorphology' : [ "animate", "set" ],
'feOffset' : [ "animate", "set" ],
'feSpecularLighting' : [ "desc", "title", "metadata", "feDistantLight", "fePointLight", "feSpotLight" ],
'feTile' : [ "animate", "set" ],
'feTurbulence' : [ "animate", "set" ],
'font' : [ "desc", "title", "metadata", "font.face", "glyph", "hkern", "missing.glyph", "vkern" ],
'glyph' : [ "animate", "set", "animateMotion", "animateColor", "animateTransform", "desc", "title", "metadata", "rect", "circle", "ellipse", "line", "polyline", "polygon", "path", "svg", "g", "defs", "symbol", "use", "linearGradient", "radialGradient", "a", "altGlyphDef", "clipPath", "color.profile", "cursor", "filter", "font", "font.face", "foreignObject", "image", "marker", "mask", "pattern", "script", "style", "switch", "text", "view" ],
'missing.glyph' : [ "animate", "set", "animateMotion", "animateColor", "animateTransform", "desc", "title", "metadata", "rect", "circle", "ellipse", "line", "polyline", "polygon", "path", "svg", "g", "defs", "symbol", "use", "linearGradient", "radialGradient", "a", "altGlyphDef", "clipPath", "color.profile", "cursor", "filter", "font", "font.face", "foreignObject", "image", "marker", "mask", "pattern", "script", "style", "switch", "text", "view" ],
'hkern' : [ "Empty." ],
'vkern' : [ "Empty." ],
'font.face' : [ "desc", "title", "metadata", "font.face.src" ],
'font.face.src' : [ "font.face.name", "font.face.uri" ],
'font.face.uri' : [ "font.face.format" ],
'font.face.format' : [ "Empty." ],
'font.face.name' : [ "Empty." ],
'linearGradient' : [ "desc", "title", "metadata", "animate", "animateTransform", "set", "stop" ],
'radialGradient' : [ "desc", "title", "metadata", "animate", "animateTransform", "set", "stop" ],
'stop' : [ "animate", "animateColor", "set" ],
'pattern' : [ "animate", "set", "animateMotion", "animateColor", "animateTransform", "desc", "title", "metadata", "rect", "circle", "ellipse", "line", "polyline", "polygon", "path", "svg", "g", "defs", "symbol", "use", "linearGradient", "radialGradient", "a", "altGlyphDef", "clipPath", "color.profile", "cursor", "filter", "font", "font.face", "foreignObject", "image", "marker", "mask", "pattern", "script", "style", "switch", "text", "view" ],
'marker' : [ "animate", "set", "animateMotion", "animateColor", "animateTransform", "desc", "title", "metadata", "rect", "circle", "ellipse", "line", "polyline", "polygon", "path", "svg", "g", "defs", "symbol", "use", "linearGradient", "radialGradient", "a", "altGlyphDef", "clipPath", "color.profile", "cursor", "filter", "font", "font.face", "foreignObject", "image", "marker", "mask", "pattern", "script", "style", "switch", "text", "view" ],
'path' : [ "animate", "set", "animateMotion", "animateColor", "animateTransform", "desc", "title", "metadata" ],
};

}

start=programme

NL      =   '\r'? '\n' 
EOF = !.


expr_seperator =  ( comment NL / NL / ';')
expr_list= _ ( expr (___ expr_seperator _ expr)* )?

programme = expr_list _ EOF

// TODO: replace binary_expr with expr???
expr = svgRCall / binary_expr 
 
// TODO: add svgR rules
//      define containers, shapes, ...., and rules of membership

atomic_expr = 
    ID / 
    STRINGLITERAL /
    HEX /
    COMPLEX /
    FLOAT /
    INT /
    'NULL' /
    'NA' /
    'Inf' /
    'NaN' /
    'TRUE' /
    'FALSE' /
    'next' /
    'break'
  
// TODO: ALERT for missing }. ),   RULES: {, (
//       ALERT for missing (  RULES :for, while,function
left_token_expr =  
    '{' _ expr_list _ '}'   /                          
    'if' ___ '(' _ expr _ ')' _ expr ___ 'else' _ expr /
    'if' ___ '(' _ expr _ ')' _ expr /
    'for' ___ '(' _ ID _ 'in' _ expr _ ')' _ expr /
    'while' ___ '(' _ expr _ ')' _ expr /
    'repeat' _ expr /
    '(' _ expr _ ')' /
    'function' ___ '(' _ formals? _ ')' _ expr  /
    ('+' / '-') expr
  
  
//TODO: ALERT: formals, extra commas or missing commmas
formal= ID ___ '=' _ expr / ID / '...'

formalCombo1 =  (',' _ formal)

formalCombo2 = ( _ (','/formal)) {
    addError( "comma issues", location() ); 
    return "comma";
}

formals= formal (___ (formalCombo1 / formalCombo2))* 


//Todo: ALERT: parameters, extra commas or missing commas
parameter= ID ___ '=' _ expr/ expr / '...'

paramCombo1=(',' _ parameter)

paramCombo2=( _ (','/parameter)) { 
    addError( "comma issues", location() ); 
    return "comma";
}


//parameters= parameter (___ ',' _ parameter)* 
parameters= parameter (___ (paramCombo1/paramCombo2))* 
        


slist_expr =   ___ '[' _ sublist _ ']'  
dlist_expr =   ___ '[[' _ sublist _ ']]'  
call_expr=  '(' _ parameters? _ ')'

composite_expr = ( left_token_expr / atomic_expr ) ( !expr_seperator ( dlist_expr / slist_expr / call_expr  )  )* 

svgRCall = head:keySVGR '(' _ tail:svgRparameters? _ ')' 
{
   
    var i;  // todo check each tail element and see if ok with head
    if(tail){
        for( i=0; i< tail.length; i++){
            if(allElements.hasValue(tail[i]) )
                if(!acceptContentEle[head].hasValue(tail[i])){
                // if bad, record in warnings ow do nothing
                addWarning( tail[i] + " not in content model of " + head, location() );
                break;
            }
        }
    }
    // in either case, return head
    return head;
}

//Todo: ALERT: parameters, extra commas or missing commas
svgRparameter= ID ___ '=' _ expr/ expr / '...'

//This returns to return only svg token
svgRparamCombo1= ',' _ tail:svgRparameter 
{
    return tail;
}

svgRparamCombo2=( _ tail:(','/svgRparameter)) { 
    addError( "comma issues", location() ); 
    return tail;
}

svgRparamCombo12 = ___ tail:(svgRparamCombo1/svgRparamCombo2)
{
    return tail;
}

//parameters= parameter (___ ',' _ svgRparameter)* 

// returns an array of potential svgR tokens
svgRparameters= head:svgRparameter tail:(svgRparamCombo12)* 
{
    var result = tail;
    result.unshift(head);
    return result;
}
 

// call_expr

OP_SYMBOL = [><:+&*\-.$=] / LETTER


bin_op = '%' OP_SYMBOL*'%'/ '<<-' / '->>' /':::' /
  '<-' / '==' /'::' / '>=' / '!=' / '||' / '&&' / ':=' /'<=' /  '->' / 
  '$' / '@' / '^' / ':' / '*' / '/' / '+' / '-' / '>' /  '<' /   '&' /  '|' / '~'  / '=' 

binary_expr = ( composite_expr ) (  ___ bin_op  _  expr  ___  )*

 
//LineTerminator
LineTerminator = [\u000A\u000D\u2028\u2029]

// WhiteSpace
WS = [\u0009\u000B\u000C\u0020\u00A0\uFEFF\u1680\u180E\u2000-\u200A\u202F\u205F\u3000]

// Optional WhiteSpace
_ = (WS / LineTerminator / comment)*

// Optional WhiteSpace with no line breaks.
__ = (WS /  comment)*

___ = WS*


LETTER  = [a-zA-Z] 


keyWord = 'function' / 'if' / 'for' / 'while' / 'repeat' / 'next' / 'in'
idword  =  ( LETTER / ( '.' (LETTER/'_'/'.') ) ) (LETTER/DIGIT/'_'/'.')*

keySVGR = "feComponentTransfer"/ "feSpecularLighting"/ "feDiffuseLighting"/ "feDisplacementMap"/ "animateTransform"/ "feConvolveMatrix"/ "font.face.format"/ "feDistantLight"/ "feGaussianBlur"/ "font.face.name"/ "linearGradient"/ "radialGradient"/ "animateMotion"/ "color.profile"/ "feColorMatrix"/ "font.face.src"/ "font.face.uri"/ "foreignObject"/ "missing.glyph"/ "altGlyphItem"/ "animateColor"/ "feMorphology"/ "fePointLight"/ "feTurbulence"/ "altGlyphDef"/ "feComposite"/ "feMergeNode"/ "feSpotLight"/ "font.face"/ "altGlyph"/ "clipPath"/ "feOffset"/ "glyphRef"/ "metadata"/ "polyline"/ "textPath"/ "animate"/ "ellipse"/ "feBlend"/ "feFlood"/ "feFuncA"/ "feFuncB"/ "feFuncG"/ "feFuncR"/ "feImage"/ "feMerge"/ "pattern"/ "polygon"/ "circle"/ "cursor"/ "feTile"/ "filter"/ "marker"/ "script"/ "switch"/ "symbol"/ "glyph"/ "hkern"/ "image"/ "mpath"/ "style"/ "title"/ "tspan"/ "vkern"/ "defs"/ "desc"/ "font"/ "line"/ "mask"/ "path"/ "rect"/ "stop"/ "text"/ "tref"/ "view"/ "set"/ "svg"/ "use"/ "a"/ "g"

ID = !(keyWord) idword


DIGIT=  [0-9]
INT =   DIGIT+ [Ll]? 
EXP =   ('E' / 'e') ('+' / '-')? INT

FLOAT=  DIGIT+ '.' DIGIT* EXP? [Ll]? /
    DIGIT+ EXP? [Ll]? /
    '.' DIGIT+ EXP? [Ll]?
    
COMPLEX=    INT 'i' / FLOAT 'i'

HEX =   '0' ('x'/'X') HEXDIGIT+ [Ll]?
HEXDIGIT = ([0-9]/[a-f]/[A-F]) 
HEX_ESCAPE = '\\' HEXDIGIT HEXDIGIT? 
OCTAL_ESCAPE =  '\\' [0-3] [0-7] [0-7]
    /   '\\' [0-7] [0-7]
    /   '\\' [0-7] 
    
UNICODE_ESCAPE = '\\' 'u' HEXDIGIT HEXDIGIT HEXDIGIT HEXDIGIT /
    '\\' 'u' '{' HEXDIGIT HEXDIGIT HEXDIGIT HEXDIGIT '}' 

ESC =   '\\' [abtnfrv"'\\] /
  UNICODE_ESCAPE /
  HEX_ESCAPE /
  OCTAL_ESCAPE


sub =  expr / '...' 
subg = (sub !sub) / ','
sublist = _ (subg _)+
 

STRINGLITERAL =
    '"' DOUBLESTRINGCHARACTER* '"' /
    "'" SINGLESTRINGCHARACTER* "'"
    
DOUBLESTRINGCHARACTER =
    !(["] / "\\" ) .
    
SINGLESTRINGCHARACTER =
    !(['] / "\\") .
  

comment =   '#' (!(NL/EOF) .)*


